#!/usr/bin/env escript
%% -*- erlang -*-

-include_lib("kernel/include/file.hrl").

%% shamelessly stolen from rebar
main(Args) ->
    %% Get a string repr of build time
    Built = build_time(),

    %% Check for force=1 flag to force a rebuild
    case lists:member("force=1", Args) of
        true ->
            [file:delete(Beam) || Beam <- filelib:wildcard("ebin/*.beam")],
            ok;
        false ->
            ok
    end,
    
    AppVsn =
        %% Make sure file:consult can parse the .app file
        case file:consult("ebin/grackle.app") of
            {ok,[{_,_,Props}]} ->
                proplists:get_value(vsn, Props);
            {error, Reason} ->
                io:format("Invalid syntax in ebin/grackle.app: ~p\n", [Reason]),
                halt(1)
        end,
        
    MakeOpts = [
        {outdir, "ebin"}, 
        {i, "include"},
        {d, 'APP_VSN', AppVsn}
    ],
    %% Compile all src/*.erl to ebin
    case make:files(filelib:wildcard("src/*.erl"), MakeOpts) of
        up_to_date ->
            ok;
        error ->
            io:format("Failed to compile source files!\n"),
            halt(1)
    end,

    %% Add ebin/ to our path
    true = code:add_path("ebin"),

    %% Construct the archive of everything in ebin/ dir -- put it on the
    %% top-level of the zip file so that code loading works properly.
    Files = filelib:wildcard("*", "ebin"),
    case zip:create("mem", Files, [{cwd, "ebin"}, memory]) of
        {ok, {"mem", ZipBin}} ->
            %% Archive was successfully created. Prefix that binary with our
            %% header and write to "grackle" file
            Script = <<"#!/usr/bin/env escript\n", ZipBin/binary>>,
            case file:write_file("grak", Script) of
                ok ->
                    ok;
                {error, WriteError} ->
                    io:format("Failed to write grak script: ~p\n", [WriteError]),
                    halt(1)
            end;
        {error, ZipError} ->
            io:format("Failed to construct grak script archive: ~p\n", [ZipError]),
            halt(1)
    end,

    %% Finally, update executable perms for our script
    file:write_file_info("grak", #file_info{mode=(8#00700)}),

    %% Add a helpful message
    io:format("Successfully generated a grak executable in the current working directory~n").
                
build_time() ->
    {{Y, M, D}, {H, Min, S}} = calendar:now_to_universal_time(now()),
    lists:flatten(io_lib:format("~4..0w~2..0w~2..0w_~2..0w~2..0w~2..0w", [Y, M, D, H, Min, S])).
            

